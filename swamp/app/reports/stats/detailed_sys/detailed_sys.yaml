docs:
  metrics.detailed_sys:
    # location: Архитектура в цифрах/Доля целевых систем
    description: Доля внутренних ИТ-приложений компании, имеющих детальное описание архитектуры в DocHub
    type: markdown
    author: v.kozlov
    template: detailed_sys.md
    origin: samolet.dataset.sys_metrics
    source: >
      (
        $systems := $.systems;
        $tu_blocks := $.tu_blocks;
        $components := $.components;
        $component_list := $components.$spread().($keys());

        $tu_block_id := $params.tu_block_id;

        $tu_blocks_and_totals := $append([{"id": "total", "title": "Общий итог"}], $tu_blocks);
        $tu_blocks_and_totals := $tu_block_id ? $tu_blocks_and_totals[id=$tu_block_id] : $tu_blocks_and_totals;
        
        $tu_blocks_statistics := $tu_blocks_and_totals.(
          $block := $; 
          $block_id := $block.id;
          $tu_block_systems := ($block_id = "total") ? $systems : $systems[tu_block.tu_id = $block_id];
          $sys_list := $tu_block_systems.id;
          $ext_systems_pattern := $get_ext_systems_pattern();
          $ext_sys_list := $filter($sys_list, function ($v) { $v ~> $ext_systems_pattern });

          $detailed_sys_list:= $sys_list.(
            $system_id := $;
            $system_id_pattern := $eval("/^" & $system_id & "/");
            $components_count := $count($filter($component_list, function ($v) { $v ~> $system_id_pattern}));
            ($components_count > 0)
              ? {
                "id": $system_id,
                "components": $components_count
              };
          );

          $ext_sys_count := $count($ext_sys_list);
          $inter_sys_count := $count($sys_list) - $ext_sys_count;
          $detailed_sys_count := $count($detailed_sys_list);
          $non_detailed_sys_count := $inter_sys_count - $detailed_sys_count;
          
          $non_detailed_sys_link := $tu_block_id = "total"
          ? "[Детализация по ИТ-командам](/entities/docs/blank?dh-doc-id=metrics.detailed_sys)"
          : "[Список систем не имеющих детальное описание архитектуры: " & $non_detailed_sys_count & "](/entities/docs/blank?dh-doc-id=metrics.non_detailed_sys_list&tu_block_id=" & $block_id & ")";
          
          {
            "tu_block": $block.title,
            "internal": $inter_sys_count,
            "external": $ext_sys_count,
            "total": $count($sys_list),
            "detailed": $detailed_sys_count,
            "detailed_cover": ($inter_sys_count > 0) ? $round((($detailed_sys_count / $inter_sys_count) * 100),2) : 0,
            "non_detailed_sys_link": $non_detailed_sys_link
          };
        );

        {
          "unit_statistics": $tu_blocks_statistics
        };
      )

  metrics.non_detailed_sys_list:
    description: Список систем не имеющих детальное описание архитектуры в DocHub
    type: table
    headers:
      - value: title
        text: Система
        sortable: true
        align: left
        width: 10%
        link: link_to_system
        
      - value: short_description
        text: Краткое описание
        sortable: false
        align: left
        width: 30%

      - value: system_status
        text: Статус системы
        sortable: true
        align: left
        width: 10%

      - value: architect
        text: Архитектор
        sortable: true
        align: left
        width: 10%

      - value: tu_block
        text: ИТ-команда
        sortable: true
        align: left
        width: 10%

    origin: samolet.dataset.sys_metrics
    source: >
      (        
        $systems := $.systems;
        $tu_blocks := $.tu_blocks;
        $components := $.components;
        $component_list := $components.$spread().($keys());
        
        $tu_block_id := $params.tu_block_id;        

        $tu_block_systems := ($tu_block_id = "total") ? $systems : $systems[tu_block.tu_id = $tu_block_id];
        $sys_list := $tu_block_systems.id;
        $ext_systems_pattern := $get_ext_systems_pattern();
        $ext_sys_list := $filter($sys_list, function ($v) { $v ~> $ext_systems_pattern });

        $non_detailed_sys_list := $sys_list.(
          $system_id := $;
          $is_ext_system := $contains($system_id, $get_ext_systems_pattern());          
          $is_ext_system = false
          ? (
              $system_id_pattern := $eval("/^" & $system_id & "/");
              $components_count := $count($filter($component_list, function ($v) { $v ~> $system_id_pattern}));
              $components_count = 0
              ? (
                  $tu_block_systems[id = $system_id].
                  {
                    "title": title,
                    "short_description": short_description,
                    "system_status": system_status,
                    "architect": architect.title,                    
                    "tu_block": tu_block.title,
                    "link_to_system": "/architect/components/" & id
                  };
                  
                );
            );
        );

      )